plugins {
    id 'com.android.library'
    id "org.jetbrains.kotlin.android"
//    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'maven-publish'
    id 'signing'
    id 'org.asciidoctor.jvm.convert'
    id 'org.jetbrains.dokka'
}

repositories {
    google()
    mavenCentral()
    mavenLocal()
}

ext.admin = hasProperty('sonatypeUsername')

project.ext.versionCode = getVersionCode()
version = getVersionName()
group = domain

android {

    namespace 'com.ingonoka.cba9driver'

    compileSdk android_compile_sdk_version.toInteger()

    defaultConfig {

        minSdkVersion android_min_sdk_version
        targetSdkVersion android_target_sdk_version.toInteger()
        versionCode = getVersionCode()
        versionName = version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            debuggable = false
        }
        debug {
            debuggable = true
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
//        freeCompilerArgs += ['-Xdebug']
    }

    publishing {
        multipleVariants {
            withSourcesJar()
            withJavadocJar()
            allVariants()
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation("org.jetbrains.kotlinx:kotlinx-datetime:$androidx_datetime_version")

    implementation "org.slf4j:slf4j-api:$slf4j_version"
    testImplementation 'junit:junit:4.13.2'

    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

}

tasks.register("sourceJar", Jar) {
    from android.sourceSets.main.kotlin.srcDirs
    classifier 'sources'
}

tasks.register('dokkaJar', Jar) {
    dependsOn(dokkaHtml)
    from dokkaHtml.outputDirectory
}

dokkaJar.dependsOn(dokkaHtml)

artifacts {
    archives dokkaJar
}


// See configuration on ~/.gradle/gradle.properties
signing {
    sign(publishing.publications)
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact(sourceJar)
                artifactId = 'cba9-driver'
            }
            debug(MavenPublication) {
                from components.debug
                artifact(sourceJar)
                artifactId = 'cba9-driver-debug'

            }
        }
    }
}

publishing {
    publications.configureEach {
        pom {
            name = 'CBA9 Banknote Acceptor Driver Library'
            description = 'A library implementing the SSP protocol for CBA9 Banknopte Acceptors'
            url = 'https://github.com/ingonoka/cba9-driver'
            licenses {
                license {
                    name = pom_licenseName
                    url = pom_licenseUrl
                }
            }
            scm {
                connection = 'scm:git:git@github.com:ingonoka/cba9-driver'
                developerConnection = 'scm:git:git@github.com:ingonoka/cba9-driver'
                url = 'https://github.com/ingonoka/cba9-driver'
            }
            developers { developer { name = pom_developer } }
        }
    }

    repositories {

        maven {
            name = "Sonatype"
            url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
            credentials {
                username = sonatype_uid
                password = sonatype_pw
            }
        }
    }
}

dokkaHtmlPartial {
    moduleName.set("CBA9 Banknote Acceptor Driver Library")
    dokkaSourceSets {
        configureEach {
            includes.from("module.md")
        }
    }
}
